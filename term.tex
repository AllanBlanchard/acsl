\begin{syntax}
  bin-op ::= "+" | "-" | "*" | "/" | "%" ;
       | "==" | "!=" | "<=" | ">=" | ">" | "<" ;
       | "&&" | "||" |   ; boolean operations
       | "&" | "|" | {"-->"} | {"<-->"} | "^"; bitwise operations
       \
  unary-op ::= "+" | "-" ; unary plus and minus
       | "!" ; boolean negation
       | "~" ;  bitwise complementation
       | "*" ; pointer dereferencing
       | "&" ; address-of operator
       \
  term ::= "\true" | "\false" ;
       | integer ; integer constants
       | real ; real constants
       | id ; variables
       | unary-op term ;
       | term bin-op term ;
       | term "[" term "]" ; array access
       | {"{" term "for" "[" term "]" "=" term "}"} ; array functional modifier
       | term "." id  ; structure field access
       | {"{" term "for" "."id "=" term "}"} ; field functional modifier
       | term "->" id ;
       | "(" type-expr ")" term  ; cast
       | id "(" term ("," term)* ")" ; function application
       | "(" term ")" ; parentheses
       | term "?" term ":" term ; ternary condition
       | "\let" id "=" term ";" term ; local binding
       | "sizeof" "(" term ")" ;
       | "sizeof" "(" C-type-expr ")" ;
       | id ":" term ; syntactic naming
       \
  rel-op ::= "==" | "!=" | "<=" | ">=" | ">" | "<"
       \
  pred ::= "\true" | "\false" ;
       | term (rel-op term)+ ; comparisons (see remark)
       | id "(" term ("," term)* ")" ; predicate application
       | "(" pred ")" ; parentheses
       | pred "&&" pred ; conjunction
       | pred "||" pred ; disjunction
       | pred "==>" pred ; implication
       | pred "<==>" pred ; equivalence
       | "!" pred ; negation
       | pred "^^" pred ; exclusive or
       | term "?" pred ":" pred ; ternary condition
       | pred "?" pred ":" pred;
       | "\let" id "=" term ";" pred ; local binding
       | "\let" id "=" pred ";" pred ;
       | "\forall" binders ";" pred ; universal quantification
       | "\exists" binders ";" pred ; existential quantification
       | id ":" pred ; syntactic naming
\end{syntax}

%%% Local Variables:
%%% mode: latex
%%% TeX-master: "main"
%%% End:

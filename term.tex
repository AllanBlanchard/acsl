\begin{syntax}
  bin-op ::= "+" | "-" | "*" | "/" | "%" ; 
       | "==" | "!=" | "<=" | ">=" | ">" | "<" ;
       | "&" | "|" | "=>" | "<=>" | "^"; bitwise operations 
       \
  unary-op ::= "+" | "-" ; unary plus and minus
       | "!" | "~" ;  
       | "*" ; C pointer dereferencing
       | "&" ; C address operator
       \
  term ::= id ;
       | unary-op term ;
       | term bin-op term ;  
       | term "." id  ; structure field access
       | term "->" id ; 
       | "(" \tau ")" term  ; cast 
       | term "[" term "]" ; array access
       | id "(" term ("," term)* ")" ; function application
       | "(" term ")" ; parentheses
       | term "?" term ":" term ; 
       | "\let" id "=" term ";" term ; local naming
       | "sizeof" "(" term ")" ; 
       | "sizeof" "(" \tau ")" ;
       | "offsetof" "(" ... ")" ;
       | "alignof" "(" \tau ")" ;
       | id "::" term ; syntactic naming 
       | "(" term ("," term)+ ")" ; tuples
       | "\let" "(" id (, id)+ ")" "=" term ";" term ; \experimental
       \
  rel-op ::= "==" | "!=" | "<=" | ">=" | ">" | "<" 
       \
  pred ::= "\true" | "\false" ;
       | term (rel-op term)* ; comparisons, see remark below
       | id "(" term ("," term)* ")" ; predicate application
       | "(" pred ")" ; parentheses
       | pred "&&" pred ; conjunction
       | pred "||" pred ; disjunction
       | pred "==>" pred ; implication
       | pred "<==>" pred ; equivalence
       | "!" pred ; negation
       | pred "^^" pred ; exclusive or
       | term "?" pred ":" pred ;
       | pred "?" pred ":" pred ; 
       | "\let" id "=" term ";" pred ; local naming
       | "\forall" binders ";" pred ; universal quantification
       | "\exists" binders ";" pred ; existential quantification
       | id "::" pred ; syntactic naming 
\end{syntax}

%%% Local Variables:
%%% mode: latex
%%% TeX-master: "main"
%%% End:
